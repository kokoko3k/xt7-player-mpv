' Gambas class file

'Copyright (C) 2007, 2008 Antonio Orefice
' Gambas class file



Public Sub Form_Open()
'logarea.text = File.Load("/tmp/text")
  Global.Center(Fmain, Me)
  ShowLogs()
  searchterm.SetFocus()
End

Public Sub OkBTN_Click()
  Me.hide
  Wait 0.1
  If FMain.mplayer.ProcessRunningOvr() Then fmain.videoboxproxy.SetFocus()
End

Public Sub RefreshBTN_Click()
  ShowLogs()
  searchterm_Click()
  searchterm2_Click()
  Wait
End



Public Sub clearlogs()
  Try logarea.Clear
  Try ModulesArea.clear
End

Public Struct mpvOption 
  name As String
  data As String
End Struct

Public Sub ShowLogs()
  Dim name, data As String

  streamtextbox.text = FMain.mplayer.getP("fullpath")
  Logarea.Clear
  ModulesArea.clear
  
  logarea.text &= "Commandline:\n"
  logarea.text &= FMain.mplayer.commandline & "\n\n"
  
  logarea.text &= "Options:\n"

  For Each FMain.mplayer.mpvOptions
    name = FMain.mplayer.mpvOptions.Key
    logarea.text &= "  --" & Trim(name)
    data = FMain.mplayer.mpvOptions[name]
    If data <> "" Then logarea.text &= "=" & data
    logarea.text &= "\n"
  Next
  logarea.text &= "\n"
  
  logarea.text &= FMain.mplayer.stdout
  ModulesArea.text &= FMain.mplayer.modules_out
  Try logarea.pos = CInt(logarea.tag)
  Try modulesarea.pos = CInt(modulesarea.tag)
  logarea.EnsureVisible()
  modulesarea.EnsureVisible()
End




Public Sub ScrollTo(text As String, logarea As Textedit)
  Dim p As Integer
  p = InStr(logarea.text, text, CInt(logarea.tag + 1), gb.IgnoreCase)
  If (p = 0) And (logarea.tag <> 0) Then 
    logarea.tag = 0 
    p = InStr(logarea.text, text, CInt(logarea.tag + 1))
  Endif
  logarea.scrolly = 9999999
  logarea.scrollx = 9999999
  logarea.pos = p
  logarea.EnsureVisible
  logarea.scrolly -= 20
  logarea.scrollx -= 20
  Wait
  logarea.tag = p
End



Public Sub searchterm_Click()
  HighLight(logarea, searchterm)
  ScrollTo(Last.text, logarea)
End


Public Sub searchterm2_Click()
  HighLight(ModulesArea, searchterm2)
  ScrollTo(Last.text, ModulesArea)
End

Public Sub Searchterm_change()
  Dim previous_search As String = searchterm.text
  Wait 0.5 'fixme, this used to work in the past, now slows down.
  If previous_search <> searchterm.text Then Return
  HighLight(logarea, searchterm)
End


Public Sub Searchterm_Keyrelease()
  If (key.code = Key.return) Or (Key.code = Key.enter) Then
    ScrollTo(searchterm.text, logarea)
    Return
  Endif
End

Public Sub Searchterm2_change()
 Dim previous_search As String = searchterm2.text
 Wait 0.5
 If previous_search <> searchterm2.text Then Return
 HighLight(ModulesArea, searchterm2)
End


Public Sub Searchterm2_Keyrelease()
  If (key.code = Key.return) Or (Key.code = Key.enter) Then
    ScrollTo(searchterm2.text, ModulesArea)
    Return
  Endif
End


Public Sub Highlight(logarea As Object, searchbox As ButtonBox)
  Dim sx, sy As Integer
  If (Trim(searchbox.text) = "") Then 
    searchbox.background = Color.textBackground
    Return
  Endif
  If DoHighlight(logarea, searchbox.text) Then
    searchbox.background = Color.textBackground
      Else
    searchbox.background = Color.red
  Endif
End


Private Function DoHighlight(logarea As Object, foundstr As String) As Boolean
  Dim lenfoundstr As Integer = 0
  Dim rtext As String
  Dim hlight1 As String = "<font color=red><b>"
  Dim hlight2 As String = "</b></font>"
  Dim i, sfound As Integer
  Dim found As Boolean = False
  Dim MyLeft, MyMid, MyRight As String
  lenfoundstr = Len(foundstr)
  logarea.EnsureVisible
  rtext = logarea.text
  i = 0  
  rtext = Replace(logarea.text, "\n", "<br>", gb.IgnoreCase)
  Repeat
    sfound = InStr(rtext, foundstr, i, gb.IgnoreCase)
    If sfound = 0 Then Break
    myleft = Left(rtext, sfound - 1) 
    mymid = hlight1 & Mid(rtext, sfound, lenfoundstr) & hlight2
    myright = Mid(rtext, sfound + lenfoundstr, Len(rtext))
    rtext = myleft & mymid & myright
    i = Len(myleft) + Len(mymid) + 1
    found = True
  Until (sfound = 0) Or (i >= Len(rtext))

  logarea.RichText = rtext  
  Return found
End 
